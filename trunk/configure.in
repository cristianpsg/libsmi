#
# configure.in --
#
#      Process this file with autoconf to produce a configure script.
#
# Copyright (c) 1999 Frank Strauss, Technical University of Braunschweig.
#
# See the file "COPYING" for information on usage and redistribution
# of this file, and for a DISCLAIMER OF ALL WARRANTIES.
#
# @(#) $Id: configure.in,v 1.48 1999/12/22 14:43:57 strauss Exp $
#

AC_INIT(tools/smilint.c)
AM_INIT_AUTOMAKE(libsmi,0.1.9-pre5)
LIBTOOL_VERSIONING="1:2:0"
# NOTE: Though minor interface changes may take place between libsmi patch
# levels (last number in AM_INIT_AUTOMAKE), we increment the libtool
# versioning revision (first number in LIBTOOL_VERSIONING) only along with
# major and minor revision changes (first and second number in
# AM_INIT_AUTOMAKE).

AC_DEFINE(BACKEND_SMI)
AC_DEFINE(BACKEND_SMING)

AC_ARG_WITH(cflags,
[  --with-cflags=FLAGS     use FLAGS for CFLAGS],
CFLAGS="$withval")

AC_PROG_CC
AC_ARG_WITH(cc,
[  --with-cc=CC            use CC as the C compiler],
CC="$withval")

AC_CHECK_FUNC(strtoll, AC_DEFINE(HAVE_STRTOLL))
AC_CHECK_FUNC(strtoull, AC_DEFINE(HAVE_STRTOULL))
AC_CHECK_FUNC(strtoq, AC_DEFINE(HAVE_STRTOQ))
AC_CHECK_FUNC(strtouq, AC_DEFINE(HAVE_STRTOUQ))

AC_CHECK_HEADER(getopt.h, AC_DEFINE(HAVE_GETOPT_H))
AC_CHECK_HEADER(pwd.h, AC_DEFINE(HAVE_PWD_H))

AC_DEFINE(DEFAULT_GLOBALCONFIG, "/usr/local/etc/smi.conf")
AC_DEFINE(DEFAULT_USERCONFIG, ".smirc")

AC_ARG_WITH(mibdir,
[  --with-mibdir=DIR       use DIR to install libsmi MIB modules [DATADIR/mibs]],
mibdir="$withval", mibdir="/usr/local/share/mibs")

AC_ARG_WITH(smipath,
[  --with-smipath=DIR:DIR  default DIRs to search for MIB modules [MIBDIR...]],
smipath="$withval", smipath="$mibdir/ietf:$mibdir/iana:$mibdir/irtf:$mibdir/site")
AC_DEFINE_UNQUOTED(DEFAULT_SMIPATH, "$smipath")


AC_ARG_ENABLE(smi,
[  --enable-smi            enable SMIv1/v2 parser support],
AC_DEFINE(BACKEND_SMI))

AC_ARG_ENABLE(sming,
[  --enable-sming          enable SMIng parser support],
AC_DEFINE(BACKEND_SMING))

AC_PATH_PROG(FLEX, "flex")
if test -z "${FLEX}" ; then
  echo "NOTE: We will not be able to build scanner C code from flex sources."
fi

AC_PATH_PROG(BISON, "bison")
if test -z "${BISON}" ; then
  echo "NOTE: We will not be able to build parser C code from bison sources."
fi

AC_PATH_PROG(BASH, "bash")
AC_PATH_PROG(SH, "sh")
AC_PATH_PROG(AWK, awk)

AC_EXEEXT
AC_PROG_INSTALL
AC_PROG_MAKE_SET
AM_PROG_LIBTOOL
AC_SUBST(LIBTOOL_DEPS)

if ${CONFIG_SHELL} ./libtool --features | grep "enable static" >/dev/null; then
  STATIC=-static
else
  STATIC=
fi
AC_SUBST(STATIC)

AC_SUBST(LIBTOOL_VERSIONING)
AC_SUBST(EXEEXT)
AC_SUBST(BASH)
AC_SUBST(SH)
AC_SUBST(AWK)
AC_SUBST(CC)
AC_SUBST(CFLAGS)
AC_SUBST(mibdir)
AC_SUBST(smipath)

AC_OUTPUT(Makefile lib/Makefile tools/Makefile mibs/Makefile mibs/iana/Makefile mibs/ietf/Makefile mibs/irtf/Makefile mibs/site/Makefile test/Makefile test/mibs/Makefile test/dumps/Makefile doc/Makefile lib/libsmi.3 lib/smi_macro.3 lib/smi_module.3 lib/smi_node.3 lib/smi_type.3 lib/smi_config.3 tools/smistrip tools/smiquery.1 tools/smilint.1 tools/smidump.1 tools/smistrip.1 test/parser.test, chmod a+x test/parser.test)
